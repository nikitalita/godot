#!/usr/bin/env python

import default_theme_icons_builders
import default_theme_builders
from platform_methods import run_in_subprocess
import glob

Import("env")


env.add_source_files(env.scene_sources, "*.cpp")
if env["fallback_fonts"]:
    flist = glob.glob(env.Dir("#thirdparty").abspath + "/fonts/*.ttf")
    flist.extend(glob.glob(env.Dir("#thirdparty").abspath + "/fonts/*.otf"))
    flist.extend(glob.glob(env.Dir("#thirdparty").abspath + "/fonts/*.woff"))
    flist.extend(glob.glob(env.Dir("#thirdparty").abspath + "/fonts/*.woff2"))
    flist.sort()
    env.Depends("#scene/resources/default_theme/default_font.gen.h", flist)
    env.CommandNoCache(
        "#scene/resources/default_theme/default_font.gen.h",
        flist,
        run_in_subprocess(default_theme_builders.make_fonts_header),
    )
else:
    env.Depends("#scene/resources/default_theme/default_font.gen.h",
                "#thirdparty/fonts/OpenSans_SemiBold.woff2")
    env.CommandNoCache(
        "#scene/resources/default_theme/default_font.gen.h",
        "#thirdparty/fonts/OpenSans_SemiBold.woff2",
        run_in_subprocess(default_theme_builders.make_fonts_header),
    )

env["BUILDERS"]["MakeDefaultThemeIconsBuilder"] = Builder(
    action=env.Run(
        default_theme_icons_builders.make_default_theme_icons_action, "Generating default project theme icons header."
    ),
    suffix=".h",
    src_suffix=".svg",
)

# Default theme icons
icon_sources = Glob("*.svg")

env.Alias(
    "default_theme_icons",
    [env.MakeDefaultThemeIconsBuilder("#scene/resources/default_theme/default_theme_icons.gen.h", icon_sources)],
)
